10 REM Cit86 to Net69 Packet Translator
20 REM
30 NUL$ = CHR$(0): FF$ = CHR$(255): MS$ = CHR$(255) + CHR$(0)
50 OPEN "SH86BCH.000" FOR INPUT AS #1
60 OPEN "MSG.69" FOR OUTPUT AS #2
65 OPEN "MESG.TMP" FOR OUTPUT AS #3
70 PRINT #2, "Sinkhole"; NUL$;
80 CTY$ = "": PHN$ = "": PTH$ = "": DD$ = "": D1$ = "": S1$ = "": S2$ = "": DOH$ = "": TT$ = ""
85 M& = 0: S& = 0: H& = 0: D& = 0: TN$ = "": ADR$ = "":Q$=""
90 A$ = "": D$ = "": C$ = "": N$ = "": X$ = "": O$ = "": R$ = "": S$ = "": M$ = "": T$ = "": Z$ = ""
100 IF EOF(1) GOTO 3000
101 IN$ = INPUT$(1, #1)
110 IF IN$ = "A" GOTO 150
112 IF IN$ = "D" GOTO 200
114 IF IN$ = "C" GOTO 250
116 IF IN$ = "N" GOTO 300
118 IF IN$ = "X" GOTO 350
120 IF IN$ = "O" GOTO 400
122 IF IN$ = "R" GOTO 450
124 IF IN$ = "S" GOTO 500
126 IF IN$ = "T" GOTO 550
127 IF IN$ = "Q" GOTO 575
130 IF IN$ = "M" GOTO 650
140 GOTO 700
150 IN$ = INPUT$(1, #1)
155 IF IN$ = NUL$ THEN GOTO 100
160 A$ = A$ + IN$
165 GOTO 150
200 IN$ = INPUT$(1, #1)
205 IF IN$ = NUL$ THEN GOTO 100
210 D$ = D$ + IN$
215 GOTO 200
250 IN$ = INPUT$(1, #1)
255 IF IN$ = NUL$ THEN GOTO 100
260 C$ = C$ + IN$
265 GOTO 250
300 IN$ = INPUT$(1, #1)
305 IF IN$ = NUL$ THEN GOTO 100
310 N$ = N$ + IN$
315 GOTO 300
350 IN$ = INPUT$(1, #1)
355 IF IN$ = NUL$ THEN GOTO 100
360 X$ = X$ + IN$
365 GOTO 350
400 IN$ = INPUT$(1, #1)
405 IF IN$ = NUL$ THEN GOTO 100
410 O$ = O$ + IN$
415 GOTO 400
450 IN$ = INPUT$(1, #1)
455 IF IN$ = NUL$ THEN GOTO 100
460 R$ = R$ + IN$
465 GOTO 450
500 IN$ = INPUT$(1, #1)
505 IF IN$ = NUL$ THEN GOTO 100
510 S$ = S$ + IN$
515 GOTO 500
550 IN$ = INPUT$(1, #1)
555 IF IN$ = NUL$ THEN GOTO 100
560 T$ = T$ + IN$
565 GOTO 550
575 IN$ = INPUT$(1, #1)
580 IF IN$ = NUL$ THEN GOTO 100
585 Q$ = Q$ + IN$
590 GOTO 575
650 IN$ = INPUT$(1, #1)
655 IF IN$ = NUL$ THEN GOTO 800
660 M$ = M$ + IN$
665 GOTO 650
700 IN$ = INPUT$(1, #1)
705 IF IN$ = NUL$ THEN GOTO 100
710 DOH$ = DOH$ + IN$
715 GOTO 700
800 GOSUB 4000
805 GOSUB 5000
810 CTY$ = LEFT$(O$, 2)
819 IF MID$(O$, 3, 1) <> " " THEN PHN$ = MID$(O$, 3, 1)
820 FOR P = 4 TO LEN(O$): PHN$ = PHN$ + MID$(O$, P, 1): NEXT P
830 IF N$ = "Sinkhole" THEN PTH$ = "Sinkhole" ELSE PTH$ = N$ + "!..!Sinkhole"
840 IF T$ <> "" THEN GOTO 6000
900 IF ROOM$ = R$ THEN GOTO 1000
910 IF ROOM$ <> R$ THEN ROOM$ = R$
920 PRINT #2, NUL$; ROOM$; NUL$;
1000 PRINT #2, MS$;
1010 PRINT #2, S$; NUL$;
1020 PRINT #2, "A"; A$; NUL$;
1030 PRINT #2, "D"; DD$; NUL$;
1040 PRINT #2, "O"; N$; NUL$;
1050 PRINT #2, "S"; S$; NUL$;
1060 PRINT #2, "o"; X$; NUL$;
1070 PRINT #2, "P"; PTH$; NUL$;
1080 PRINT #2, "Q"; CTY$; NUL$;
1090 PRINT #2, "H"; PHN$; NUL$;
1100 PRINT #2, "M"; M$; NUL$;
1110 GOTO 80
3000 CLOSE #1: CLOSE #2: CLOSE #3
3010 IF ISMAIL = 0 THEN SHELL "DEL MESG.TMP"
3020 SYSTEM
4000 DD$ = MID$(D$, 6, 2): YY$ = MID$(D$, 1, 2): MM$ = MID$(D$, 3, 3)
4010 IF MM$ = "Jan" THEN M& = 0
4011 IF MM$ = "Feb" THEN M& = 31
4012 IF MM$ = "Mar" THEN M& = 59
4013 IF MM$ = "Apr" THEN M& = 90
4014 IF MM$ = "May" THEN M& = 120
4015 IF MM$ = "Jun" THEN M& = 151
4016 IF MM$ = "Jul" THEN M& = 181
4017 IF MM$ = "Aug" THEN M& = 212
4018 IF MM$ = "Sep" THEN M& = 243
4019 IF MM$ = "Oct" THEN M& = 273
4020 IF MM$ = "Nov" THEN M& = 304
4021 IF MM$ = "Dec" THEN M& = 334
4030 IF YY$ = "95" THEN D& = 9130
4040 IF YY$ = "96" AND M& < 59 THEN D& = 9495
4050 IF YY$ = "96" AND M& > 59 THEN D& = 9496
4060 D& = D& + VAL(DD$) + M&
4070 S& = D& * 86400
4075 HOUR$ = "": MIN$ = ""
4080 FOR P = 1 TO LEN(C$)
4081 SP = P
4082 IF MID$(C$, P, 1) <> ":" THEN HOUR$ = HOUR$ + MID$(C$, P, 1) ELSE GOTO 4084
4083 NEXT P
4084 FOR P = (SP + 1) TO LEN(C$)
4085 IF MID$(C$, P, 1) <> " " THEN MIN$ = MIN$ + MID$(C$, P, 1) ELSE GOTO 4090
4086 NEXT P
4090 IF RIGHT$(C$, 2) = "pm" AND VAL(HOUR$) < 12 THEN H& = VAL(HOUR$) + 12 ELSE H& = VAL(HOUR$)
4091 H& = H& + 5: H& = H& * 3600
4095 M& = VAL(MIN$): M& = M& * 60
4100 S& = S& + H& + M&
4105 D1$ = STR$(S&): DD$ = ""
4106 FOR P = 2 TO LEN(D1$): DD$ = DD$ + MID$(D1$, P, 1): NEXT P
4115 RETURN
5000 ST$ = S$: S$ = "": S = 0: S1$ = "": S2$ = ""
5005 IF LEFT$(ST$, 1) = "0" THEN FOR P = 3 TO LEN(ST$): S$ = S$ + MID$(ST$, P, 1): NEXT P: RETURN
5010 FOR P = 1 TO LEN(ST$)
5011 SP = P
5013 IF MID$(ST$, P, 1) <> " " THEN S1$ = S1$ + MID$(ST$, P, 1) ELSE GOTO 5020
5015 NEXT P
5020 FOR P = (SP + 1) TO LEN(ST$): S2$ = S2$ + MID$(ST$, P, 1): NEXT P
5030 S = VAL(S1$) * 65535: S = S + VAL(S1$): S = S + VAL(S2$)
5033 ST$ = "": ST$ = STR$(S)
5035 FOR P = 2 TO LEN(ST$): S$ = S$ + MID$(ST$, P, 1): NEXT P
5040 RETURN
6000 IF UCASE$(T$) <> "GATEWAY" GOTO 6160
6010 IF LEFT$(M$, 1) <> "#" THEN GOTO 6340
6020 FOR P = 1 TO LEN(M$)
6025 IF P = 1 AND MID$(M$, P, 1) = "#" THEN GOTO 6050
6030 IF P > 1 AND MID$(M$, P, 1) = "#" THEN GOTO 6055
6040 ADR$ = ADR$ + MID$(M$, P, 1)
6050 NEXT P
6055 IF LEN(ADR$) > 80 THEN GOTO 6340
6056 TR=P
6060 FOR P = 1 TO LEN(ADR$)
6070 IF MID$(ADR$, P, 1) = "@" THEN GOTO 6100
6080 TN$ = TN$ + MID$(ADR$, P, 1)
6090 NEXT P
6100 IF RIGHT$(TN$, 1) = " " THEN TN$ = LEFT$(TN$, LEN(TN$) - 1)
6105 T$ = TN$
6110 S = P + 1
6120 FOR P = S TO LEN(ADR$)
6130 IF P = S AND MID$(ADR$, P, 1) = " " THEN GOTO 6150
6140 Z$ = Z$ + MID$(ADR$, P, 1)
6150 NEXT P
6152 M$ = RIGHT$(M$, LEN(M$)-TR)
6155 GOTO 6200
6160 IF Q$="" THEN Z$ = "BrickCitHouse" ELSE Z$=Q$
6200 ISMAIL=1:PRINT #3, MS$;
6210 PRINT #3, S$; NUL$;
6220 PRINT #3, "A"; A$; NUL$;
6230 PRINT #3, "D"; DD$; NUL$;
6240 PRINT #3, "O"; N$; NUL$;
6250 PRINT #3, "S"; S$; NUL$;
6260 PRINT #3, "o"; X$; NUL$;
6270 PRINT #3, "P"; PTH$; NUL$;
6280 PRINT #3, "Q"; CTY$; NUL$;
6285 PRINT #3, "R"; R$; NUL$;
6290 PRINT #3, "T"; T$; NUL$;
6300 PRINT #3, "Z"; Z$; NUL$;
6310 PRINT #3, "H"; PHN$; NUL$;
6320 PRINT #3, "M"; M$; NUL$;
6340 GOTO 80

